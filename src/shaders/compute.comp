#version 450
#extension GL_ARB_separate_shader_objects : enable

#define WORKGROUP_SIZE 32

#define GRAVITY_ON 1
#define RECOVER_ON 1
#define WIND_ON 1

#define ORIE_CULL_ON 1
#define DIST_CULL_ON 1
#define VIEW_CULL_ON 1

layout(local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform CameraBufferObject {
    mat4 view;
    mat4 proj;
    vec3 pos;

} camera;

layout(set = 1, binding = 0) uniform Time {
    float deltaTime;
    float totalTime;
};

struct Blade {
    vec4 v0;
    vec4 v1;
    vec4 v2;
    vec4 up;
};

// TODO: Add bindings to:
// 1. Store the input blades
layout(set = 2, binding = 0) buffer Blades {
    Blade blades[];
} ;

// 2. Write out the culled blades
layout(set = 2, binding = 1) buffer CulledBlades {
    Blade culled_blades[];
} ;
// The project is using vkCmdDrawIndirect to use a buffer as the arguments for a draw call
// This is sort of an advanced feature so we've showed you what this buffer should look like
//

// 3. Write the total number of blades remaining
layout(set = 2, binding = 2) buffer NumBlades {
 	  uint vertexCount;   // Write the number of blades remaining here
 	  uint instanceCount; // = 1
 	  uint firstVertex;   // = 0
 	  uint firstInstance; // = 0
} numBlades;

bool inBounds(float value, float bounds) {
    return (value >= -bounds) && (value <= bounds);
}

// helper for frustum culling 
bool inFrustum(vec3 point)
{
    mat4 vp = camera.proj * camera.view;
    vec4 pp = vp * vec4(point, 1.0f);
    float tolerance = 0.2f;
    float h = pp.w + tolerance;
    return inBounds(pp.x, h) && inBounds(pp.y, h) && inBounds(pp.z, h);
}

#define DIST_CULL_MAX 30
#define GRAVITY vec4(0, -1, 0, 4.9)
#define windFunc (vec3(2.0, 4.0, 2.0) * sin(totalTime))



void main() {
	// Reset the number of blades to 0
	if (gl_GlobalInvocationID.x == 0) {
		numBlades.vertexCount = 0;
	}
	barrier(); // Wait till all threads reach this point

    // TODO: Apply forces on every blade and update the vertices in the buffer
    uint id = gl_GlobalInvocationID.x;
    Blade blade = blades[id];
    vec3 v0 = blade.v0.xyz;
    vec3 v1 = blade.v1.xyz;
    vec3 v2 = blade.v2.xyz;
    vec3 up = blade.up.xyz;

    float direction = blade.v0.w;
    float height = blade.v1.w;
    float width = blade.v2.w;
    float stiffness = blade.up.w;
    vec3 orient = vec3(cos(direction), 0, sin(direction)); 

    // gravity
    vec3 gravity = vec3(0,0,0);
    #if GRAVITY_ON
        vec3 gE = normalize(GRAVITY.xyz) * GRAVITY.w;
        vec3 frontDir = normalize(cross(orient, up));
        vec3 gF = 0.25f * length(gE) * frontDir;
        gravity = gE + gF;  
    #endif

    // recovery
    vec3 recovery = vec3(0,0,0);
    #if RECOVER_ON
        vec3 iv2 = v0 + up * height;
        recovery = (iv2 - v2) * stiffness;
    #endif

    // wind
    vec3 wind = vec3(0,0,0);
    #if WIND_ON
        float windDirection = 1 - abs(dot(normalize(windFunc), normalize(v2 - v0)));
        float windAlign = dot((v2 - v0), up) / height;
        wind = windFunc * windDirection * windAlign;
    #endif

    vec3 totalForce = gravity + recovery + wind;

    v2 += totalForce * deltaTime;
    v2 -= up * min(dot(up, v2 - v0), 0); 

    float Lproj = length(v2 - v0 - up * dot(v2 - v0, up));
    v1 = v0 + height * up * max(1 - Lproj / height, 0.05 * max(Lproj/height, 1));
    
    float L0 = length(v2 - v0);
    float L1 = length(v2 - v1) + length(v1 - v0);
    float L = (2 * L0 + (2 - 1) * L1) / (2 + 1);
    float r = height / L;

    v1 = v0 + r * (v1 - v0);
    v2 = v1 + r * (v2 - v1);

    blades[id].v1 = vec4(v1, blade.v1.w);
    blades[id].v2 = vec4(v2, blade.v2.w);

	// TODO: Cull blades that are too far away or not in the camera frustum and write them
	// to the culled blades buffer
	// Note: to do this, you will need to use an atomic operation to read and update numBlades.vertexCount
	// You want to write the visible blades to the buffer without write conflicts between threads
    // Reset the number of blades to 0

    vec3 cam_pos = inverse(camera.view)[3].xyz;

    #if ORIE_CULL_ON
        if (abs(dot(normalize(cam_pos - v0), orient)) >= 0.9)
            return;
    #endif

    #if VIEW_CULL_ON
        vec3 m = 0.25 * v0 + 0.5 * v1 + 0.25 * v2;
        if(!(inFrustum(v0) && inFrustum(v2) && inFrustum(m)))
             return;
    #endif

    #if DIST_CULL_ON
        vec3 dir = v0 - cam_pos;
        float dProj = length(dir - up * dot(dir, up));
        if (mod(id, 10.0) >= (10.0 * (1 - dProj / DIST_CULL_MAX)))
           return;
    #endif

    culled_blades[atomicAdd(numBlades.vertexCount, 1)] = blades[id];
}